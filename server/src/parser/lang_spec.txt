// program
program -> declaration*

// declarations
declaration -> ( statement
    | variableDeclaration
    | lockDeclaration
    | functionDeclaration
    | parameterDeclaration
)

variableDeclaration -> scope idenitifer ( 'to' | 'is' ) expression '.'
lockDeclaration -> scope? 'lock' idenitifer 'to' expression '.'
parameterDeclaration -> scope? 'parameter' parameters '.'
functionDeclaration -> scope? 'function' statementBlock '.'?

// declaration components
scope -> 'declare'? ('local' | 'global')?

// parameters
parameters -> ( parameter (',' parameters)? 
    | defaultParameters
)
defaultParameters -> defaultParameter (',' defaultParameters)?
parameter -> ( idenitifer | defaultParameter )
defaultParameter -> idenitifer ( 'to' | 'is' ) expression

// statements
statement -> ( statementBlock
    | identifierInstruction
    | commandstatement
    | commandExpressionstatement
    | unset
    | unlock
    | set
    | lazyGlobal
    | if
    | until
    | from
    | when
    | break
    | return
    | switch
    | for
    | on
    | toggle
    | wait
    | log
    | copy
    | rename
    | delete
    | run
    | runPath
    | runOncePath
    | compile
    | list
    | empty
    | print
)

// directive
lazyGlobal -> '@' 'lazyglobal' onOff '.'

// block
statementBlock -> '{' declarations* '}'

// control flow
if -> 'if' expression statement '.'? ('else' statement '.'?)? 
until -> 'until' expression statement '.'?
from -> 'from' statementBlock 'until' expression 'step' statementBlock 'do' statement
when -> 'when' expression 'then' statement '.'?
break -> 'break' '.'
return -> 'return' expression? '.'
for -> 'for' idenitifer 'in' suffix statement '.'?
on -> 'on' suffix statement
toggle -> 'toggle' suffix '.'
wait -> 'wait' 'until'? expression '.'

// variables
set -> 'set' suffix 'to' expression '.'
unset -> 'unset' ( 'all' | idenitifer ) '.'
unlock -> 'unlock' ( 'all' | idenitifer ) '.'

// io
copy -> 'copy' expression ( 'to' | 'from' ) expression '.'
switch -> 'switch' 'to' expression '.'
log -> 'log' expression 'to' expression '.'
rename -> 'rename' ( 'volume' | 'file' ) ioIdentifier expression 'to' expression '.'
delete -> 'delete' expression ('from' expression)? '.'
compile -> 'compile' expression ('to' expression)? '.'
run -> 'run' 'once'? ( string | fileIdentifier ) ('(' arguments ')')? ('on', expresion)? '.'
runPath -> 'runPath' '(' expression (',' arguments)? ')' '.'
runOncePath -> 'runOncePath' '(' expression (',' arguments)? ')' '.'
print -> 'print' expression ('at' '(' expression ',' expression ')')? '.'

// misc
empty -> '.'
list -> 'list' (idenitifer ('in' idenitifer)?)? '.'

// command statement
commandExpressionstatement -> commandExpression expression '.'
commandstatement -> command '.'

// identifier statements
identifierInstruction -> suffix ( 'on' | 'off' )? '.'

// expressions
expression -> or | statementBlock

// binary expresions
or -> and ('or' and)?
and -> equality ('and' equality)?
equality -> comparison (( '=' | '<>' ) comparison)?
comparison -> addition (( '<' | '>' | '<=' | '>=' ) addition)?
addition -> multiplication (( '+' | '-' ) multiplication)?
multiplication -> unary (( '*' | '/' ) unary)?

// unary expressions
unary -> ( '+' | '-' | 'not' | 'defined' )? factor
factor -> suffix ('^' suffix)*
suffix -> suffixTerm (':' suffix)*
suffixTerm -> atom (suffixTermTrailer* delegate?)
suffixTermTrailer -> (call 
    | arrayIndex 
    | arrayBracket
)
call -> '(' arguments ')'
arrayIndex -> '#' ( idenitifer | integer )
arrayBracket -> '[' expression ']'
delegate -> '@' 

// expresion components
arguments -> expression? (',' expression)*

// atom
atom -> ( literal
    | fileIdentifier
    | idenitifer
    | '(' expression ')'
)

// literal
literal -> ( number
    | string
    | 'true'
    | 'false'
)

idenitifer -> ([all valid letter] | '_')*
fileIdentifier -> idenitifer ('.' idenitifer)*

string -> '"' [utf-16]* '"'
integer -> sub_number
double -> base ('e' ('+' | '-') sub_number)?
base -> (sub_number 
    | '.' sub_number 
    | sub_number '.' sub_number
)

sub_number -> [0-9] ([0-9_]* [0-9])*

// lists
command -> ( 'stage' 
    | 'clearscreen' 
    | 'preserve' 
    | 'reboot' 
    | 'shutdown'
)
commandExpression -> ( 'edit' 
    | 'add' 
    | 'remove'
)
ioIdentifier -> ( 'file' | 
    'volume' 
) 
number -> ( integer |
    double
)

